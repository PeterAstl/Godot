extends Node2D

signal left_mouse_button_clicked
signal left_mouse_button_released

const COLLISION_MASK_CARD = 1
const COLLISION_MASK_DECK = 4

var card_manager_reference
var deck_reference

func _ready() -> void:
	card_manager_reference = $"../CardManager"
	deck_reference = $"../PlayerDeck"

func _input(event):
	if event is InputEventMouseButton and event.button_index == MOUSE_BUTTON_LEFT:
		if event.pressed:
			emit_signal("left_mouse_button_clicked")
			raycast_at_cursor()
		else:
			emit_signal("left_mouse_button_released")

func raycast_at_cursor():
	var space_state = get_world_2d().direct_space_state
	var parameters = PhysicsPointQueryParameters2D.new()
	parameters.position = get_global_mouse_position()
	parameters.collide_with_areas = true
	parameters.collision_mask = COLLISION_MASK_CARD | COLLISION_MASK_DECK

	var result = space_state.intersect_point(parameters)
	if result.size() == 0:
		return

	var collider = result[0].collider
	if collider.collision_mask & COLLISION_MASK_CARD != 0:
		var card_found = collider.get_parent() as Card
		if card_found:
			card_manager_reference.start_drag(card_found)

	elif collider.collision_mask & COLLISION_MASK_DECK != 0:
		deck_reference.draw_card()
